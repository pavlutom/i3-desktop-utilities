#!/bin/zsh


zmodload zsh/mathfunc

# define some constants
HW_LOWEST=200;		KEY_LOWEST='lowest'
HW_LOW=400;			KEY_LOW='low'
HW_MEDIUM=1500;		KEY_MEDIUM='Medium'
HW_HIGH=12000;		KEY_HIGH='HIGH'
HW_MAX=24000;		KEY_MAX='MAX'

# setup increase/decrease
KEY_INC='inc'
KEY_DEC='dec'
integer HW_STEP=$(( $HW_MAX * 0.1 ))

# output constants
if [[ $1 == 'keys' ]]; then
	echo -e "$KEY_LOWEST\n$KEY_LOW\n$KEY_MEDIUM\n$KEY_HIGH\n$KEY_MAX"
	exit 0
fi

# load hardware
source ~/.scripts/.display/.names
DEV='--device=intel_backlight'

# get inc/dec
HW_CUR=$(brightnessctl $DEV get)
if [[ $1 == $KEY_INC ]]; then
	# treat low values
	if [[ $HW_CUR -lt HW_LOWEST ]]; then
		HW_REQ=$HW_LOWEST
	elif [[ $HW_CUR -lt HW_LOW ]]; then
		HW_REQ=$HW_LOW
	elif [[ $HW_CUR -lt HW_MEDIUM ]]; then
		HW_REQ=$HW_MEDIUM
	# add 10%
	else
		integer HW_REQ=$(( floor(($HW_CUR + $HW_STEP) / $HW_STEP) * $HW_STEP ))
		# do not exceed max
		if [[ $HW_REQ -gt $HW_MAX ]]; then
			HW_REQ=$HW_MAX
		fi
	fi

elif [[ $1 == $KEY_DEC ]]; then
	# lower by 10%
	if [[ $HW_CUR -gt $(( $HW_MEDIUM + $HW_STEP )) ]]; then
		integer HW_REQ=$(( ceil(($HW_CUR - $HW_STEP * 1.0) / $HW_STEP) * $HW_STEP ))
	# treat low values
	elif [[ $HW_CUR -gt $HW_MEDIUM ]]; then
		HW_REQ=$HW_MEDIUM
	elif [[ $HW_CUR -gt $HW_LOW ]]; then
		HW_REQ=$HW_LOW
	else
		HW_REQ=$HW_LOWEST
	fi

# get requested value
elif [[ $1 == $KEY_LOWEST ]]; then
	HW_REQ=$HW_LOWEST
elif [[ $1 == $KEY_LOW ]]; then
	HW_REQ=$HW_LOW
elif [[ $1 == $KEY_MEDIUM ]]; then
	HW_REQ=$HW_MEDIUM
elif [[ $1 == $KEY_HIGH ]]; then
	HW_REQ=$HW_HIGH
elif [[ $1 == $KEY_MAX ]]; then
	HW_REQ=$HW_MAX
fi

# get brightness percentage
if [[ $HW_REQ ]]; then		# calculate from requested value
	HW_BR=$HW_REQ
	FRAC=$(( $HW_REQ / 24000.0 ))
	integer BR=$(( rint( $FRAC * 100 ) ))
else						# parse from input
	BR=$(~/.scripts/.utils/parsepercent $1)
	FRAC=$(($BR / 100.0))
	integer HW_BR=$(( rint($FRAC * $HW_MAX) ))
fi

# if we can use laptop display backlight
if [[ $BR -ge 0 && $BR -le 100 ]]; then
	# set xrandr to 100%
	xrandr --output $M0 --brightness 1
	# use hardware
	brightnessctl $DEV set $HW_BR
else
	# set hardware to 100%
	brightnessctl $DEV set $HW_MAX
	# use xrandr
	xrandr --output $M0 --brightness $FRAC
fi

# external monitors (always using xrandr)
FRAC_EXT=$(( $FRAC ** 0.25 ))
xrandr --output $M1 --brightness $FRAC_EXT
xrandr --output $M2 --brightness $FRAC_EXT

# notify
NOTIF_ID=42069	# arbitrary id (different for every kind of notification)
dunstify -u 0 -t 1500 -r $NOTIF_ID "Brightness $BR %"
